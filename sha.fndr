#!/mnt/z/gits/Fender/target/debug/fender
@import util
@import logic
@import math

@export *

$u32 = 4294967295

$left_rotate = (n, b) {
    "Left rotate a 32-bit integer n by b bits."
    util::bitwise_and(
        util::bitwise_or(
            math::lshift(n, b),
            math::rshift(n, 32 - b)
        ),
        u32)
    }

$chunks = (iterable, size) {
    "Chunks the given iterable into parts of a given size"
    $out = []
    $idx = 0
    while ({idx < iterable.len()}, {
        $temp = []
        while ({temp.len() < size}, {
            temp.push(iterable[idx + temp.len()])
        })
        out.push(temp)
        idx += size
    })
    out
}

$sha1 = (m) {
    "computes the SHA-1 Hash digest of a given string"
    $h0 = 1732584193 # 0x67452301
    $h1 = 4023233417 # 0xEFCDAB89
    $h2 = 2562383102 # 0x98BADCFE
    $h3 = 271733878  # 0x10325476
    $h4 = 3285377520 # 0xC3D2E1F0

    $bits = []
    $n = 0
    while({n < m.len()}, {
        $character = util::int2bin(int(m[n]))
        character = util::leftpad(character, 8, 0)
        util::extend(bits, character)
        n += 1
    })

    bits.push(1)

    $pbits = util::copy(bits)

    while ({pbits.len() % 512 != 448}, {
        pbits.push(0)
    })

    util::extend(pbits, util::leftpad(
        util::int2bin(bits.len() - 1), 64, 0)
    )
    $_c = 0
    $inital = chunks(pbits, 512)
    while ({_c < inital.len()}, {

        $words = chunks(inital[_c], 32)
        $w = util::zeros(80)

        n = 0

        while ({n < 16}, {
            w[n] = util::binarr2int(words[n])
            n += 1
        })

        $i = 16

        while ({i < 80}, { 
            $_s1 = util::bitwise_xor(w[i-3], w[i-8])
            $_s2 = util::bitwise_xor(_s1, w[i-14])
            $_s3 = util::bitwise_xor(_s2, w[i-16])
            w[i] = left_rotate(_s3, 1)
            i += 1
        })

        $a = h0
        $b = h1
        $c = h2
        $d = h3
        $e = h4
        
        i = 0

        while ({i < 80}, {
            $f = 0
            $k = 0

            (bool(logic::and(0 <= i, i <= 19))).then({
                $inner = util::bitwise_and(b, util::bitwise_xor(c, d))
                f = util::bitwise_xor(d, inner)
                k = 1518500249
            })

            (bool(logic::and(20 <= i, i <= 39))).then({
                f = util::bitwise_xor(
                        util::bitwise_xor(b, c), d
                )
                k = 1859775393
            })

            (bool(logic::and(40 <= i, i <= 59))).then({
                $left = util::bitwise_and(b, c)
                $middle = util::bitwise_and(b, d)
                $right = util::bitwise_and(c, d)
                f = util::bitwise_xor(
                        util::bitwise_xor(left, middle),
                    right)
                k = 2400959708
            })

            (bool(logic::and(60 <= i, i <= 79))).then({
                f = util::bitwise_xor(
                        util::bitwise_xor(b, c), d
                )
                k = 3395469782
            })

            $temp = util::bitwise_and(
                left_rotate(a, 5) + f + e + k + w[i],
                u32
            )

            e = d
            d = c
            c = left_rotate(b, 30)
            b = a
            a = temp

            i += 1
        })

        h0 = util::bitwise_and(a + h0, u32)
        h1 = util::bitwise_and(b + h1, u32)
        h2 = util::bitwise_and(c + h2, u32)
        h3 = util::bitwise_and(d + h3, u32)
        h4 = util::bitwise_and(e + h4, u32)

        _c += 1
    })

    $final = [h0, h1, h2, h3, h4]
    util::each(final, util::int2hex)
    util::join(final, "")
}

sha1("")